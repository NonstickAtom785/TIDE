VPutS_loc:
  ld (textRow),bc
  jr VPutS

_:
  inc hl
  call VPutC
VPutS:
;Inputs:
;   HL points to a zero terminated string to display
;Outputs:
;   string is displayed
;   HL points to the ending byte
;   A is 0
;   z flag is set
;   c flag reset
;Destroys:
;     DE,BC
  ld a,(hl)
  or a
  jr nz,-_
  ret

VPutC_newline:
  bit scrollrow,(iy+editorflags)
  jr z,+_
  ;can't destroy HL, but A is 0
  ld (row_left),a
  ld (row_left+1),a
_:
  ld (textcol),a
  ld a,(textrow)
  cp 64
  ret nc
  add a,6
  ld (textrow),a

  ret

VPutC_tab:
  ld a,(textcol)
  add a,16
  and %11110000
  bit scrollrow,(iy+editorflags)
  jr z,+_
  cp TEXTBOX_WIDTH
  jr c,+_
  ;then we need to shift the text row left by A-textcol pixels
  push hl
  ld hl,(textrow)
  sub h
  push af
  call shift_text_row_left
  pop af
  ;need to add A to row_left
  ld hl,(row_left)
  add a,l
  ld l,a
  jr nc,$+3
  inc h
  ld (row_left),hl
  pop hl

  ;need to set textcol to TEXTBOX_WIDTH
  ld a,TEXTBOX_WIDTH
_:
  ld (textcol),a
  ret

VPutC_str:
  ld e,(hl)
  inc hl
  ld d,(hl)
  inc hl
  push hl
  ex de,hl
  jr +_
VPutC_strrel:
  ld c,(hl)
  inc hl
  ld b,(hl)
  inc hl
  push hl
  add hl,bc
_:
  call VPutS
  pop hl
  ret

VPutC_token:
  ret
VPutC_invert:
  ld a,(flags+editorflags)
  xor 1<<textinverse
  ld (flags+editorflags),a
  ret

VPutC:
;Inputs:
;    A is the char to draw
;     (textRow) is the pixel row to draw at
;     (textCol) is the text column to draw at (0 to 23)

;Control Characters
;   0x00
;   0x01  newline
;   0x02  tab
;   0x03  str, followed by 2-byte ptr
;   0x04  str_rel, followed by 2-byte offset
;   0x05  token
;   0x06  invert



  dec a
  jr z,VPutC_newline
  dec a
  jr z,VPutC_tab
  dec a
  jr z,VPutC_str
  dec a
  jr z,VPutC_strrel
  dec a
  jr z,VPutC_token
  dec a
  jr z,VPutC_invert
  push hl
  call +_
  pop hl
  ret
_:
  ld hl,font_small+(3*6)
  ld c,a
  ld b,0
  add hl,bc
  add hl,bc
  add hl,bc

  ld de,OP1
  ld bc,$03F0  ;3*2 Nibbles
  ex de,hl
_:
  ld a,(de)
  bit textinverse,(iy+editorflags)
  jr nz,$+3
  cpl
  and c
  ld (hl),a
  inc l
  ld (hl),c
  inc l

  ld a,(de)
  bit textinverse,(iy+editorflags)
  jr nz,$+3
  cpl
  rrca \ rrca
  rrca \ rrca
  and c
  ld (hl),a
  inc l
  ld (hl),c
  inc l
  inc de
  djnz -_


  ld hl,(textrow)
  ld a,h
  bit scrollrow,(iy+editorflags)
  jr z,+_
  cp TEXTBOX_WIDTH
  jr c,+_
  push hl
  add a,4-TEXTBOX_WIDTH
  push af
  call shift_text_row_left
  pop af
  ld b,a
  ;need to add A to row_left
  ld hl,(row_left)
  add a,l
  ld l,a
  jr nc,$+3
  inc h
  ld (row_left),hl
  pop hl
  ;need to set A to TEXTBOX_WIDTH
  ld a,TEXTBOX_WIDTH-4
  ld h,a
_:

  cp 96
  ret nc
  add a,4
  ld (textcol),a
  sub 4

  ld b,6
  ld ix,OP1

;Masked Sprite routine
putsprite_masked:
;Inputs:
;   (A,L) = (x,y)
;   B is height
;   IX points to the sprite data
;       first byte is the data
;       second byte is mask
;       continues, alternating like this.
;
;Outputs:
;   Mask is ORed to the buffer, then data is XORed on top of that.
;
;Destroys:
;   AF, BC, DE, HL, IX
;
;Notes:
;   To set a pixel...
;     black: mask is 1, data is 0
;     white: mask is 1, data is 1
;     clear: mask is 0, data is 0 (keeps the data from the buffer)
;     invert: mask is 0, data is 1 (inverts the data from the buffer)
;
;This routine is free to use :)
;63 bytes (or 64 bytes if gbuf is not located at 0x**40
  ld a,l
  add a,b
  jr nc,+_
  inc ix \ inc ix \ inc l \ jr nz,$-5
  ld b,a
_;
  sub 64
  jr c,+_
  ld a,64
  sub l
  ld b,a
_:


  ld a,h
  ld e,l
  ld h,0
  ld d,h
  add hl,hl
  add hl,de
  add hl,hl
  add hl,hl
  ld e,a
  and 7
  ld c,a
  xor e  ;essentially gets E with the bottom 3 bits reset
#if (plotSScreen&255) = 64
  inc a
  rra
  rra
  rra
  ld e,a
  ld d,plotSScreen>>8
#else
  rra
  rra
  rra
  ld e,a
  add hl,de
  ld de,plotSScreen
#endif
  add hl,de

putsprite_masked_loop:
  push bc
  xor a
  ld d,(ix)
  ld e,a
  or c
  ld b,c
  ld c,e
  inc ix
  ld a,(ix)
  jr z,putsprite_masked_rotdone
putsprite_masked_rot:
  rra
  rr c
  srl d
  rr e
  djnz putsprite_masked_rot
putsprite_masked_rotdone:
  or (hl)
  xor d
  ld (hl),a
  inc hl
  ld a,(hl)
  or c
  xor e
  ld (hl),a
  ld c,11
  add hl,bc
  inc ix
  pop bc
  djnz putsprite_masked_loop
  ret
